! Written using (and testable with) GOLD Parser Builder (www.goldparser.org)

"Name"           = 'Crystal'
"Author"         = 'Chad Lechner'
"Version"        = '1.0 (WIP)'
"About"          = 'Crystal is a general-purpose, high-level, compiled language emphasizing clean, modular, reusable code.  It takes inspiration from C#, CLOS, D, F#, Haskell, Python, Ruby, Scala, and Xanadu.'

"Case Sensitive" = 'True'
"Start Symbol"   = <Start>

! ========================================
! Character Set Definitions
! ----------------------------------------

{WS}           = {Whitespace} - {CR} - {LF}                                      ! Defined as all whitespace characters except carriage return and line feed, which denote new lines
{String Char}  = {Printable} + {Printable Extended} + {HT} + {CR} + {LF} - ["]
{Name Char}    = {Printable} + {Printable Extended} - {HT} - {CR} - {LF} - ['']
{Uppercase}    = [ABCDEFGHIJKLMNOPQRSTUVWXYZ]
{Lowercase}    = [abcdefghijklmnopqrstuvwxyz]
{Hex Char}     = {Number} + [ABCDEFabcdef]

! ========================================
! "Noise" and Newline Terminals
! ----------------------------------------

Whitespace = {WS}+ !| '...' {WS}* ({CR}{LF} | {CR} | {LF} | {LS}) {WS}* ! Whitespace characters ({WS}) or a line continuation ('...')

NewLine    = {CR}{LF} | {CR} | {LF} | {LS}                             ! Carriage return, line feed, or both, or Unicode line separator

Comment Line  = '`'

! ========================================
! Keyword Terminals
! ----------------------------------------

AssignmentOperator    = '<-' | '++' | '--' | '**' | '//' | '^^^^' ('n' | 's' | 't' | '_' | '.')? ! Plain assignment, or with addition, subtraction, multiplication, division, or concatenation, respectively
ConcatenationOperator = '^^' | '^^' ('n' | 's' | 't' | '_' | '.') ! Concatenate without a joiner, or with a newline, space, tab, underscore, or dot (full stop) in between, respectively
FuncKeyword           = 'cons' | 'func'
!IdentifierKeyword     = 'args' | 'result' | 'self' | '@self'
InequalityOperator    = '~=' | '<' | '>' | '<=' | '>='
MultiplyOperator      = '*' | '/'

! ========================================
! Identifier Terminals
! ----------------------------------------

!PublicIdentifier  = {Uppercase} {AlphaNumeric}* ('-' {Uppercase} {AlphaNumeric}*)*               ! Name for public scope items
!PrivateIdentifier = {Lowercase} {AlphaNumeric}* ('-' {Lowercase} {AlphaNumeric}*)*               ! Name for private/local scoped items
Identifier        = {Letter} {AlphaNumeric}* ('-' {Letter} {AlphaNumeric}*)* | '' {Name Char}+ ''

! ========================================
! Literal Value Terminals
! ----------------------------------------
                         
StringLiteral        = '!'? '"' ({String Char} | '""' )* '"'
FloatingPointLiteral = '.' {Number}+ | {Number}+ '.' {Number}+ | '$' ({Number} ((({Number} | ',')+ | ({Number} | ' ')+) {Number})? '.'? | '.' ((({Number} | ',')+ | ({Number} | ' ')+) {Number})? | {Number} ((({Number} | ',')+ | ({Number} | ' ')+) {Number})? '.' {Number} ((({Number} | ',')+ | ({Number} | ' ')+) {Number})?)
ScientificLiteral    = ('.' {Number}+ | {Number}+ '.' {Number}+) ('e'| 'E') ('-' | '+')? {Number}+                                                                                                                                                                                                                                 ! Floating-point number in scientific notation
IntegerLiteral       = {Number}+ | '0x' {Hex Char}+

! ========================================
! Start Rule
! ----------------------------------------

<Start> ::= <nls Opt> <Program>

! ========================================
! Program Rules
! ----------------------------------------

<Program>          ::= <Implys> <Namespace Blocks>
                    |  <Implys> <Statements> <ss Opt>
                    |  <Namespace Blocks>
                    |  <Statements> <ss Opt>

<Implys>           ::= <Implys> <Imply>
                    |  <Imply>

<Imply>            ::= 'imply' <Namespaces> <ss> ! Tells the compiler to look for partially qualified (full namespace not specified) names in the listed namespaces; think of 'using' in C#

<Namespace Blocks> ::= <Namespace Blocks> <Namespace Block>
                    |  <Namespace Block>
                    
<Namespace Block>  ::= <Set Namespace> <Statements> <ss Opt>

<Set Namespace>    ::= 'in' <Full Name> <ss> ! Sets the current namespace; think of 'namespace' or 'package' in Python/Java/C#
              
<Namespaces>       ::= <Namespaces> ',' <Full Name>
                    |  <Full Name>
                 
! ========================================
! Line Rules
! ----------------------------------------

<nls>     ::= <nls> NewLine
           |  NewLine

<nls Opt> ::= <nls> ! Allow line breaks
           |  ! Nothing
              
! ========================================
! Block Rules: Generic Blocks
! ----------------------------------------

<Full Block Opt>      ::= <ss> <Statements> <ss> 'end'
                       |  <ss> 'end'

<Block Opt>           ::= <Internal Block Opt> 'end'
                   
<Internal Block Opt>  ::= <Internal Block>
                       |  <ss>
               
<Internal Block>      ::= <ss> <Internal Statements> <ss>
                   
! ========================================
! Block Rules: Control Flow
! ----------------------------------------

<If Block>      ::= 'if' <Expression> <Block Opt>
                 |  'if' <Expression> <Internal Block Opt> <Else Block>
                 |  'if' <Expression> ':' <Internal Statement>
                 |  'if' <Expression> 'loop' <Block Opt>
                 |  'if' <Expression> 'loop' <Internal Block Opt> <Else Block>
                 |  'if' <Expression> 'loop' ':' <Internal Statement>
                
<Else Block>    ::= 'else' <Block Opt>
                 |  'else' <If Block>
                 |  'else' ':' <Internal Statement>
                 
<Case Blocks>   ::= <Case Blocks> <Case Block>
                 |  <Case Block>
                 
<Case Block>    ::= 'val' <Expressions> <Internal Block Opt>            ! Match a value
                 |  'cond' <Or Condition> <Internal Block Opt>          ! Match a condition
                 |  'ref' <Expressions> <Internal Block Opt>            ! Match a reference
                 |  'type' <Full Type> <Internal Block Opt>             ! Match a datatype (for option types)
                 |  'else' <Internal Block Opt>                         ! Default case
                 |  'val' <Expressions> ':' <Internal Statement> <ss>
                 |  'cond' <Or Condition> ':' <Internal Statement> <ss>
                 |  'ref' <Expressions> ':' <Internal Statement> <ss>
                 |  'type' <Full Type> ':' <Internal statement> <ss>
                 |  'else' ':' <Internal Statement> <ss>
                    
<Or Condition>  ::= <Or Condition> 'or' <And Condition>
                 |  <And Condition>
                    
<And Condition> ::= <And Condition> 'and' <Condition>
                 |  <Condition>

! ========================================
! Statement Rules
! ----------------------------------------

! Statement Separator
<ss>                  ::= <nls> ! New lines
                       |  ';'
                       
<ss Opt>              ::= <ss>
                       |  ! Nothing

<Statements>           ::= <Statements> <ss> <Statement>
                        |  <Statement>

<Statement>            ::= <Internal Statement>
                        |  'enum' Identifier ':' <Expression>                        ! Enumeration; a set of discreet, named values, such as months, or days of the week; e.g. enum Weekdays: {Sunday, Monday, Tuesday, Wednesday, Thursday, Friday, Saturday}
                        |  'head' <Abstract Definition>                              ! Used to define members of an attribute (aka 'head type')
                        |  'require' <Type Names>                                    ! Adds the requirements of another attribute(s), e.g. 'head type #IO; require #Reader, #Writer'; or requires a specific implementation of a function set, e.g. 'head type #Number; require Add(this, this)'
                        |  'type' <Type Name> <Full Block Opt>                       ! Datatype (aka class) definition
                        |  'type' <Type Name> 'extends' <Full Type> <Full Block Opt> ! Extension module definition; extensions can: add new public members to a datatype, and override or augment existing public members of a datatype
                        |  'type' <Type Name> ':' <Full Type>                        ! Datatype alias; useful for naming attribute combinations, option types, boxed types, and types with extensions applied

<Internal Statements>  ::= <Internal Statements> <ss> <Internal statement>
                        |  <Internal Statement>
                        
<Internal Statement>   ::= <Intratype Statement>
                        |  '@' Identifier <Internal Statement>         ! e.g. @after (imperative code appended to the function), @before (imperative code prepended to the function), @SomeException, @final (try-catch finally block), @hide (private member), @implicit (implicitly-defined unit), @replace (public member override), @test (tests a pure function during compilation)
                        |  '@' Identifier <Tuple> <Internal Statement> ! e.g. @throws(Error-Disk-Read, Error-Disk-Write) (*programmer enforced* checked excpetions)

<Anonymous Block>      ::= 'code' <Block Opt> ! A code block, useful for scoping local variables, and writing inline imperative code
                  
<Intratype Statement>  ::= FuncKeyword Identifier <Func Tuple Type> '=>' <Full Type> <Block Opt>               ! Function definition with return value
                        |  FuncKeyword Identifier <Func Tuple Type> <Block Opt>                                ! Function definition with no return value
                        |  FuncKeyword <Func Tuple Type> '=>' <Full Type> <Block Opt>                          ! Nameless function/anonymous-tuple constructor, e.g. charSequence.(4) or coords: (x: 3.0, y: -1.0)
                        |  FuncKeyword <Func Tuple Type> <Block Opt>
                        |  FuncKeyword <Func Tuple Type> '=>' <Full Type> 'extends' <Full Name> <Block Opt>    ! Overloads a generic method (aka 'func set')
                        |  FuncKeyword <Func Tuple Type> 'extends' <Full Name> <Block Opt>
                        |  'measure' <Measure Identifier>                                                      ! Base measurement (e.g. length, mass, time, data) and the datatype it is associated with
                        |  'measure' <Measure Identifier> ':' <Expression>                                     ! Derived measurement (e.g. volume, force, pixel-area, voltage) defined as a combination of other measurements
                        |  'measure' <Measure Identifier> 'of' <Measure Identifier>                            ! Sub-measurement (e.g. _Width of _Length, _Height of _Length)
                        |  'state' <Measure Identifier>                                                        ! Base or default state
                        |  'state' <Measure Identifier> ':' 'apply' <Expression> 'reject' <Expressions>        ! State (e.g. negative, letter, uppercase); the 'apply' expression specifies how to reach the state (e.g. Char.ToUppercase()), and the reject expressions specify operations that invalidate the state
                        |  'prop' Identifier '=' <Full Type> <ss> 'get' <Internal Block Opt> 'set' <Block Opt> ! Getter/setter property; propeties are needed to access variables publicly; only 1 'end' keyword is used (instead of the 3 closing braces in C#) for a simpler, lighter feel
                        |  'prop' Identifier '=' <Full Type> <ss> 'get' <Block Opt>                            ! Getter (read-only) property
                        |  'prop' Identifier '=' <Full Type> <ss> 'set' <Block Opt>                            ! Setter (write-only) property
                        |  FuncKeyword Identifier <Func Tuple Type> '=>' <Full Type> ':' <Expression>          ! Mix of one-liner and block definition syntaxes
                        |  FuncKeyword Identifier <Func Tuple Type> ':' <Expression>                           ! One-line function definition
                        |  FuncKeyword <Func Tuple Type> '=>' <Full Type> ':' <Expression>
                        |  FuncKeyword <Func Tuple Type> ':' <Expression>
                        |  FuncKeyword <Func Tuple Type> '=>' <Full Type> 'extends' <Full Name> ':' <Expression> ! Overloads a generic method (aka 'func set')
                        |  FuncKeyword <Func Tuple Type> 'extends' <Full Name> ':' <Expression>
                        |  'prop' Identifier '=' <Full Type> ':' 'get' <Expression> 'set' <Identifier>         ! One-line getter/setter property
                        |  'prop' Identifier '=' <Full Type> ':' 'get' <Expression>                            ! One-line getter (read-only) property
                        |  'prop' Identifier '=' <Full Type> ':' 'set' <Identifier>                            ! One-line setter (write-only) property
                        |  'prop' Identifier ':' 'get' <Expression> 'set' <Identifier>                         ! One-line getter/setter property; useful for exposing variables publicly, as in 'prop Name: get name set name'
                        |  'prop' Identifier ':' 'get' <Expression>                                            ! One-line getter (read-only) property
                        |  'prop' Identifier ':' 'set' <Identifier>                                            ! One-line setter (write-only) property
                        !|  'unit' <Measure Identifier> '=' <Measure Identifier>                                ! Unit of measure defined as the base unit of a measure
                        |  'unit' <Measure Identifier> ':' <Concat Exps>                                       ! Unit of measure (defined as the base unit of a measure or a derived unit in terms of an existing unit(s))
                        |  'shared' <Variable Declaration>                                                     ! Variable shared among all instances of the datatype definition; i.e. 'static' in other languages
                        |  <Variable Declaration>
                        |  'build' 'shared' <Variable Declaration> <Block Opt>
                        |  'build' <Variable Declaration> <Block Opt>                                                       ! Allows you to initialize a variable, then set properties on it without specifying the variable name all of the time
                        |  <Identifiers> AssignmentOperator <Expression>                                                    ! Assignment
                        |  <Identifier> <Tuple>                                                                             ! Necessary for impure, no-result function calls
                        |  <If Block>                                                                                       ! Conditional ("if") block
                        |  'match' <Expression> <ss> <Case Blocks> 'end'                                                    ! Similar to a "switch" block
                        |  <Anonymous Block>                                                                                ! Marks a local code block; useful for scoping local variables and the 'repeat' keyword
                        |  'for' 'each' <Collection Item> <Block Opt>                                                       ! Iteration block; iterates over a collection or #Iterable
                        |  'for' 'each' <Collection Item> <Internal Block Opt> 'else' <Block Opt>                           ! Iteration block; iterates over a collection or #Iterable, executing the 'else' block if the collection/#Iterable is empty
                        |  'for' 'each' <Collection Item> ':' <Internal Statement>
                        |  'leave'
                        |  'leave' <Identifier>                                                                             ! Leaves the function call, as in a semicoroutine, keeping the stack frame and execution state
                        |  <Identifier> '<>' <Identifier>                                                                   ! Swaps the contents of the variables
                        |  'save' <Identifier>                                                                              ! Saves the current value of the variable; syntactic sugar for manipulating a temporary variable
                        |  'undo' <Identifier>                                                                              ! Reverts the variable to its saved value; syntactic sugar for manipulating a temporary variable
                        
! More Info on Measurements, Units, and States:
! Measurements define a category of interchangable units that measure the same kind of quantity, such as length, mass, or time
! Derived measurements name measurement combinations, such as area (length * legnth), or volume (length * length * length)
! Absolute measurements are measurements of an absolute qunatity, such as location, that are based on a relative measurement, such as length; absolute and relative measurements satisfy the following properties: absolute + absolute = relative, relative + relative = relative, and absolute + relative = absolute
! Sub-measurements subdivide different cases, such as height or width, of the same masurement, such as length, and are written as follows: 'name of parent' hyphen 'name of sub', e.g. _Length-Width; sub-measures may be used anywhere their ancestors are valid
! Units define standardized quantities of measurements, such as inches, meters, grams, tons, seconds, or hours, and are calculated with respect to an agreed base unit
! States enumerate discreet states of a quality, such as positive, negative, uppercase, lowercase, letter, numeral, symbol, sanitized, unsanitized, etc.
! Non-default states include an expression that converts values to that state, and list the operations that invalidate that state
! Measurements and constant units are compile-time constructs; states and variable units are analyzed at compille-time, but state and variable unit applications, which can mutate variables, occur at runtime

<Variable Declaration> ::= Identifier '=' <Full Type>                      ! Variable declaration; variables are private or local in scope; all scopes are allowed by the parser, deferring the work to semantic analysis for smarter error reporting 
                        |  Identifier '=' <Full Type> ':' <Expression>     ! Variable declaration and initialization
                        |  Identifier ':' <Expression>                     ! Variable declaration and initialization using type inference
                        |  <Collection Item>
                        |  <Collection Item> ':' <Expression>
                        
<Collection Item>      ::= Identifier 'from' <Concat Exp> ! Restricts the value of the variable to an element from an enumerator, or to a numeric range

<Abstract Definition>  ::= FuncKeyword Identifier <Func Tuple Type> '=>' <Full Type>       ! Function/constructor header, used with attributes
                        |  FuncKeyword Identifier <Func Tuple Type>
                        |  FuncKeyword 'set' Identifier <Func Tuple Type> '=>' <Full Type> ! A function set (aka generic method); use extension modules to provide concrete implementations; use 'require' syntax to require as part of an attribute
                        |  FuncKeyword 'set' Identifier <Func Tuple Type>
                        |  'prop' 'get' 'set' Identifier '=' <Full Type>                   ! Property header, used with attributes
                        |  'prop' 'get' Identifier '=' <Full Type>
                        |  'prop' 'set' Identifier '=' <Full Type>
                        |  'type' <Type Name> <Full Block Opt>                             ! Attribute (aka interface) definition
                       
! ========================================
! Expression Rules: Functional Expressions
! ----------------------------------------
              
<Expressions>    ::= <Expressions> ',' <nls Opt> <Expression>
                  |  <Expression>

<Expression>     ::= <Expression> 'if' <Lambda Exp> 'else' <Lambda Exp> ! Conditional expression
                  |  <Expression> 'else' <Lambda Exp>
                  |  <Lambda Exp>

<Lambda Exp>     ::= <Identifier> '->' <Or Exp> ! Anonymous function
                  |  <Or Exp>
                  
! ========================================
! Expression Rules: Boolean Operations
! ----------------------------------------

<Or Exps>        ::= <Or Exps> ',' <nls Opt> <Or Exp>
                  |  <Or Exp>

<Or Exp>         ::= <Or Exp> 'or' <nls Opt> <And Exp> ! Short-circuited
                  |  <And Exp>

<And Exp>        ::= <And Exp> 'and' <nls Opt> <Quantifier Exp> ! Short-circuited
                  |  <Quantifier Exp>
               
<Quantifier Exp> ::= 'any' <Cond Exp> ! Collection 'or'; returns true if at least one element is true; e.g. any IntSequence\ >= 0
                  |  'all' <Cond Exp> ! Collection 'and'; returns true if all elements are true; e.g. all IntSequence\ >= 0
                  |  <Cond Exp>

<Cond Exp>       ::= <Cond Exp> <Condition> ! Equality and inequality tests
                  |  <Concat Exp>
                    
<Condition>      ::= '=' <Concat Exp>
                  |  InequalityOperator <Concat Exp>
                  |  'is' 'within' <Concat Exp> 'of' <Concat Exp>
                  
! ========================================
! Expression Rules: Common Operators
! ----------------------------------------

<Concat Exps>    ::= <Concat Exps> ',' <nls Opt> <Concat Exp>
                  |  <Concat Exp>

<Concat Exp>     ::= <Concat Exp> ConcatenationOperator <nls Opt> <Add Exp> ! Concatenation
                  |  <Add Exp>

<Add Exp>        ::= <Add Exp> '+' <Conv Exp>  ! Addition
                  |  <Add Exp> '-' <Conv Exp>
                  |  <Conv Exp>
              
<Conv Exp>       ::= <Conv Exp> 'as' <Measure Identifier>
                  !|  <Conv Exp> 'as' <Type>
                  |  <Mult Exp>
               
<Mult Exp>       ::= <Mult Exp> MultiplyOperator <Binary Exp>
                  |  <Binary Exp>

<Binary Exp>     ::= <Binary Exp> <Full Name> <Negate Exp> ! Allows you to write out other binary operators such as 'mod', 'rsh', and so on in their natural form
                  |  <Negate Exp>

<Negate Exp>     ::= '-' <Expon Exp>       ! Negation
                  |  '~' <Expon Exp>       ! Logical not
                  |  <Expon Exp>
              
<Expon Exp>      ::= <Expon Exp> '^' <Box Exp> ! Exponentiation
                  |  <Box Exp>
                  
! ========================================
! Expression Rules: Special Expressions
! ----------------------------------------

<Box Exp>        ::= <Value> '[' <Expression> ']' ! Creates a boxed object, where the outer object can be discarded automatically as needed
                  |  <Value>
               
! More Info on Boxes:
! Boxes are generic structures for handling encapsulated data, such as IP packets, indexed elements in 'for each' loops, MP3s with IDE tags, and XML trees, to name a few
! Boxes can be nested inside each other, arbitrarily-deep, to represent structures such as IP packets and XML trees
! When passing a box to a function that uses only the inner object(s), PIE discards the encapsulating objects (from the outside in) to match the required datatype
! For instance, a box with datatype Packet[Segment[t] can be used as is anywhere a Segment[t] or t is required; to acheive this conversion PIE removes outer layers until the desired datatype is reached

! ========================================
! Value Rules: Values
! ----------------------------------------

<Value>             ::= <Identifier>
                     |  <Measure Identifiers>
                     |  <Anonymous Block>          ! Anonymous function block
                     |  <Literal> '.' <Identifier>
                     |  <Literal>
                     !|  'empty'

<Literal>           ::= '[' <Expressions> ']'                                    ! Sequence
                     |  'm[' <Matrix Expressions> ']'                            ! Matrix
                     |  'm[' <Dimensions> ']'                                    ! Empty matrix of the specified dimensions
                     |  '[' <Map Exp> ']'                                        ! Map/group/filter
                     |  'm[' <Map Exp> ']'
                     |  '[' <Map Exp> 'by' <Or Exps> ']'                         ! Sorted map/group/filter
                     |  '{' <Expressions> '}'
                     |  '{' <Lookup Pairs> '}'
                     |  '{' <Expression> 'to' <Expression> '}'                   ! Range; implicitly convertible to set, bag, range, and span objects
                     |  '{' <Expression> 'to' <Expression> 'by' <Expression> '}' ! Step-wise range (e.g. 2 to 10 by 2 produces 2, 4, 6, 8, 10)
                     |  '{' <Map Exp> '}'                                        ! Map/group/filter
                     |  StringLiteral
                     |  FloatingPointLiteral
                     |  IntegerLiteral
                     |  ScientificLiteral
                     
! ========================================
! Value Rules: Tuples
! ----------------------------------------

<Tuple>             ::= '(' <Named Expressions> ')'
                     |  '(' ')'
                     
<Named Expressions> ::= <Named Expressions> ',' <nls Opt> <Named Expression>
                     |  <Named Expression>
                      
<Named Expression>  ::= Identifier ':' <Expression>
                     |  <Expression>
                     |  Identifier ':' '_' '=' <Full Type> ! Used with partial function application to differentiate overloaded functions
                     |  '_' '=' <Full Type>
                     |  '_'

! More Info on Function Application:
! Partial function application takes a function and fills in some or all of the arguments before it is called; it is syntactic sugar for creating an anonymous function that returns a new function with fewer parameters
! Partial application occurs only when parameters remain to be filled-in later, otherwise a function call occurs; to apply all arguments to a function without calling it, use lambda syntax instead
! To use function application, specify the function, and provide a tuple containing the following: values for the arguments you're elminating, as in a function call, and underscores (with type annotations if necessary) for any remaining parameters
! To call a partial function later on, you fill-in the remaining arguments, just like a regular function call; example: messageBox.Open(title: "Unsaved Changes", msg: "Save changes?", icon: Exclamation, x: _ as Int, y: _ as Int)

! ========================================
! Value Rules: Collections
! ----------------------------------------

<Dimensions>         ::= <Dimensions> 'by' <Expression>
                      |  <Expression> 'by' <Expression>
                     
<Matrix Expressions> ::= <Matrix Expressions> ';' <nls Opt> <Expressions>
                      |  <Expressions>
                     
<Map Exp>            ::= 'for' 'each' <Names> <nls Opt> 'from' <Concat Exps> <nls Opt> <Group Filter Opt> ':' <nls Opt> <Expression>
                      
<Group Filter Opt>   ::= <Filter> <nls Opt> <Groups> <nls Opt>
                      |  <Groups> <nls Opt> <Filter> <nls Opt>
                      |  <Filter> <nls Opt>
                      |  <Groups> <nls Opt>
                      | ! Nothing
                      
<Filter>             ::= 'where' <Or Exp>

<Groups>             ::= <Groups> <Group>
                      |  <Group>
                      
<Group>              ::= 'by' <Or Exps>

<Lookup Pairs>       ::= <Lookup Pairs> ',' <nls Opt> <Expression> ':' <Expression>
                      |  <Expression> ':' <Expression>
               
! ========================================
! Identifier Rules: Identifier References
! ----------------------------------------

<Identifiers>        ::= <Identifiers> ',' <Identifier>
                      |  <Identifier>

<Identifier>         ::= <Identifier> '.' Identifier           ! Child of a parent object/namespace
                      |  <Identifier> <Tuple>                  ! The arguments of a function call
                      |  <Identifier> '.' <Tuple>              ! The index of an element or subset of a collection or tuple, using 1-based indexing
                      |  <Identifier> '.' '[' <Expression> ']' ! Gets the specified layer of a boxed object by numeric index, where 0 is the original (unboxed) object
                      |  <Identifier> '\'                      ! Gets the value of a monad
                      |  Identifier
                      |  <Tuple>                               ! Plain tuple (i.e. not a function call or element of a collection)
                      |  <Identifier> '!'                      ! Forces full evaluation; useful for ensuring side effects in logical operators

! ========================================
! Identifier Rules: Names
! ----------------------------------------

<Full Name>           ::= <Full Name> '.' Identifier ! Name + object/namespace
                       |  Identifier

<Names>               ::= <Names> ',' Identifier
                       |  Identifier
                                     
<Measure Identifiers> ::= <Measure Identifiers> <Measure Identifier> ! Annotation for units and measures
                       |  <Measure Identifier>

<Measure Identifier>  ::= '_' <Full Name>
                       |  '_' <Full Name> <Tuple> ! Absolute measure/unit; for units, the tuple specifies the offset from the relative unit; for measures, an empty tuple indicates an absolute measure
                       |  '_' <Literal>
                      
! ========================================
! Type Rules: Higher-Order Types
! ----------------------------------------

<Func Tuple Type>       ::= <Tuple Type>
                         |  '(' ')'

<Tuple Type>            ::= '(' <Tuple Type Items> <Filter> ')'
                         |  '(' <Tuple Type Items> ')'

<Tuple Type Items>      ::= <Tuple Type Items> ',' <Tuple Type Item>
                         |  <Tuple Type Item>

<Tuple Type Item>       ::= <Variable Declaration>
                         |  <Full Type>

! ========================================
! Type Rules: Type Expressions
! ----------------------------------------

<Full Type>        ::= <Option Type> '=>' <Full Type> ! The datatype of functions
                    |  <Option Type>

<Option Type>      ::= <Option Type> '|' <Collection Type>     ! Option type (i.e. one or the other); unless case matched, option types expose only the common functionality among all the types
                    |  <Collection Type>
                                        
<Collection Type>  ::= <Collection Type> '...' <Collection Item> ! Syntactic sugar for seq(t, n = count where any Set\ = n); example: 'char ... n in {1 to 255}' becomes 'seq(char, n = count where any {1 to 255}\ = n)'
                    |  <Collection Type> '...' <Value>           ! Syntactic sugar for seq(t, n = count); example: 'char ... n' becomes 'seq(char, n = count)'
                    |  <Extended Type>
                    
<Extended Type>    ::= <Extended Type> '+' <Full Name> ! A datatype with an extension module applied
                    |  <Boxed Type>

<Boxed Type>       ::= <Measured Type> '[' <Full Type> ']' ! Syntactic sugar for Box(Outer = <t1>, Inner = <t2>)
                    |  <Measured Type>
                    
<Measured Type>    ::= <Shared Type> <Measure Identifiers> ! Type with a measurement; e.g. int_length, float_mass, char_letter_case-lower, (char ... n)_xml
                    |  <Shared Type>
                    
<Shared Type>      ::= <Modified Type> '<>' ! Allows other variables to get a read/write reference to the object; the object is, in the general case, stored on the heap
                    |  <Modified Type> ':>' ! Allows other variables to get a read-only reference to the object; the object is, in the general case, stored on the heap; read-only references preserve the local functional purity of the variables's scope
                    |  <Modified Type> '<:' ! Allows other variables to get a write-only reference to the object; the object is, in the general case, stored on the heap; write-only references preserve the external functional purity of the referants' scopes
                    |  <Modified Type>      ! Prevents referencing; the object is stored directly on the stack, in CPU registers, etc. when possible

<Modified Type>    ::= '^' <Modified Type>  ! Strong reference
                    |  '@' <Modified Type>  ! Weak reference
                    |  '<:' <Modified Type> ! Read-only, used with references
                    |  ':>' <Modified Type> ! Write-only, used with references
                    |  '~' <Modified Type>  ! Queued, asynchronous access
                    |  '=' <Modified Type>  ! Constant (immutable) value
                    |  <Optional Type>
                    
<Optional Type>    ::= <Optional Type> '?'
                    |  <Type>
                    
<Attributes>       ::= <Attributes> '#' <Measured Type> ! A combination of attributes
                    |  '#' <Measured Type>              ! An attribute (abstract, structural datatype)

!<Attribute>        ::= '#' <Measured Type>    ! An attribute (abstract, structural datatype)
!                    |  !'#' 'has' <Tuple Type> ! An anonymous attribute consisting of the named items listed in the tuple

! More Info on Attributes:
! Attributes are structurally-typed interfaces; in other words, abstract (unimplemented) datatype definitions
! Attribute names always begin with a '#' (hashtag); prepending a '#' to a normal datatype creates an attribute from that datatype, or rather, invokes structural-typing with that datatype, as opposed to nominal-typing
                    
! ========================================
! Type Rules: Type Values
! ----------------------------------------
                    
<Type>           ::= <Full Name>
                  |  <Full Name> <Func Tuple Type>
                  |  <Func Tuple Type>
                  |  <Attributes> Identifier
                  |  <Attributes>
                  |  '_'                           ! Blank annotation; enables type inferrence

<Type Names>     ::= <Type Names> ',' <Type Name>
                  |  <Type Name>

<Type Name>      ::= '#' <Base Type Name>
                  |  <Base Type Name>
                    
<Base Type Name> ::= Identifier <Tuple Type>
                  |  Identifier
